suffix =
for(ever) {
    QMAKE_USE$${suffix} = $$unique(QMAKE_USE$${suffix})
    for (use, QMAKE_USE$${suffix}) {
        use = $$split(use, /)
        name = $$take_first(use)
        nu = $$upper($$name)
        debug: \
            libs = $$eval(QMAKE_LIBS_$${nu}_DEBUG)
        else: \
            libs = $$eval(QMAKE_LIBS_$${nu}_RELEASE)
        libs += $$eval(QMAKE_LIBS_$$nu)
        cflags = $$eval(QMAKE_CFLAGS_$${nu})

        isEmpty(libs):isEmpty(cflags): \
            error("Library '$$name' is not defined.")

        !contains(use, nolink): \
            LIBS$${suffix} += $$libs
        !contains(use, linkonly) {
            QMAKE_CFLAGS += $$cflags
            QMAKE_CXXFLAGS += $$cflags
        }
    }
    !isEmpty(suffix): break()
    suffix = "_PRIVATE"
}
