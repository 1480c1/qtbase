
load(default_pre)

!isEmpty(QT_VERSION) {
    qtConfig(simulator_and_device)|contains(QMAKE_MAC_SDK, ^$${device.sdk}.*): \
        CONFIG += device $${device.sdk}
    qtConfig(simulator_and_device)|contains(QMAKE_MAC_SDK, ^$${simulator.sdk}.*): \
        CONFIG += simulator $${simulator.sdk}

    qtConfig(simulator_and_device) {
        # For a simulator_and_device build all the config tests
        # are based on the device's ARM SDK, but we know that the simulator
        # is Intel and that we support SSE/SSE2.
        QT_CPU_FEATURES.$$QT_ARCH += sse sse2
        CONFIG += sse sse2
        DEFINES += QT_COMPILER_SUPPORTS_SSE2
    }
}

# Check for supported Xcode versions
lessThan(QMAKE_XCODE_VERSION, "4.3"): \
    error("This mkspec requires Xcode 4.3 or later")

ios:shared:lessThan(QMAKE_IOS_DEPLOYMENT_TARGET, "8.0"): \
    QMAKE_IOS_DEPLOYMENT_TARGET = 8.0
