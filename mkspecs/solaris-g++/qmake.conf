#
# qmake configuration for solaris-g++
#
# The X11 header files used to be broken on Solaris until patches were
# released in early 2001 for Solaris 2.6, 7, and 8. On Solaris 2.5.1
# or non-patched systems -fpermissive works around the incompatibility
# between GCC 2.95 or better and Solaris - but we still get warnings
# because we don't use -isystem.
#

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/solaris.conf)

QMAKE_LIBDIR            = /usr/sfw/lib
QMAKE_LIBDIR_X11        = /usr/openwin/lib
QMAKE_LIBDIR_OPENGL     = /usr/openwin/lib

include(../common/gcc-base.conf)

QMAKE_LFLAGS_SHLIB      = -shared
QMAKE_LFLAGS_PLUGIN     = $$QMAKE_LFLAGS_SHLIB
QMAKE_LFLAGS_SONAME     = -h$$LITERAL_WHITESPACE
QMAKE_LFLAGS_THREAD     =
QMAKE_LFLAGS_NOUNDEF    = -z defs
QMAKE_LFLAGS_RPATH      = -Wl,-R,

include(../common/g++-base.conf)

QMAKE_LFLAGS_RELEASE    =
QMAKE_LFLAGS_DEBUG      = -g

load(qt_config)
